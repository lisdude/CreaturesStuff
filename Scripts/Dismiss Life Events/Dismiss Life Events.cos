***
* Dismiss Life Events
* v1.1
*
* Dismiss all life events when you right-click on one.
*
* Author: lisdude@lisdude.com
* Last Modified: 2024-07-01
***

* - va00: The life event.
scrp 1 2 25 76
    doif _p1_ eq 2
        * Right-click, time to dismiss everything.
        inst

        * Slap the icon right out of the way!
        snde "spnk"
        targ pntr
    	frat 1
    	anim [10 11 12 11 10 0]

        enum 1 2 25
            doif targ ne null
                seta va00 targ
            	doif ov03 eq 7
            		doif ov00 ne null
			            targ ov00
            			setv ov81 1
                    endi
                endi
                kill va00
            endi
        next
    endi
endm

* Set the input mask for the life event when it's constructed.
scrp 1 2 25 10
    imsk 8
endm

rscr
scrx 1 2 25 76
scrx 1 2 25 10
