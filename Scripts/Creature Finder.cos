***
* Talk to the Hand
* v1.0
*
* Allows you to type simple commands to the hand to find specific creatures by name:
*   goto <name>		Move the camera to the creature named <name>.
*   hold <name>		Move the camera to the creature named <name> and hold its hand.
*   summon <name>		Pick up the creature named <name>, teleporting it to you.
*   get <name>		Move the camera to the creature named <name> and pick it up.
*
* Excessively commented as a learning reminder. This is my first time working with CAOS.
*
* Author: lisdude@lisdude.com
* Last Modified: 2021-05-04
***

new: simp 1 1 13816 "blnk" 0 0 0
* Set the input mask. We're only listening for Raw Key Down (1)
imsk 1
* Hide from the creatures. Nothing to see here...
attr 16

scrp 1 1 13816 73
* Wait until the user hits 'enter' (keycode 13)
	doif _p1_ eq 13

*** Variables ***
* va01: Sort of a 'break' for enums. My theory is that we do our expensive work and then set va01 to 1.
*       Then, in every other iteration of the loop, we check if va01 is true and, if so, do nothing at all.
*       Of course this is all theoretical and unverified. But it sounds good, right?
		setv va01 0
* va02: Creature name we're interested in.
		sets va02 ""
* va03: What are we doing with the creature?
*       1. Hold its hand.
*       2. Pick it up.
*       3. Move the camera to it.
*	  4. Summon it to us.
		setv va03 0
******************

* We have to wait a tick (ha! haha! hahaha!) for the speech bubble to appear, I guess.
		wait 3
* Enumerate through all speech bubbles until we find something worthwhile.
* This is moderately annoying because it will include all creature speech bubbles, not just the hand. Don't know how to do better.
		inst
		enum 1 2 9
			doif va01 eq 0 and targ ne null
				part 1
* We need to reference the length of ptxt (speech bubble text) a couple of times, so store it.
				setv va04 strl ptxt
* Check if we're being given a command and, if so, set appropriate flags. This is a lot of length checking and string splicing.
** So... does AND evaluate BOTH sides even if the left is false? This crashes if I combine the length check with the substring check... (maybe I'm missing something)
				doif va04 ge 5
					doif lowa subs ptxt 1 5 eq "hold "
* Set our 'interested name' variable (va02) to bubble[5..$] after subtracting the length of our command from the total length.
						subv va04 5
						sets va02 lowa subs ptxt 6 va04
* Set the 'hold hand' flag.
						setv va03 1
* Break variable.
						setv va01 1
					elif lowa subs ptxt 1 5 eq "goto "
						subv va04 5
						sets va02 lowa subs ptxt 6 va04
						setv va03 3
						setv va01 1
					endi
				endi
				doif va04 ge 4
					doif lowa subs ptxt 1 4 eq "get "
						subv va04 4
						sets va02 lowa subs ptxt 5 va04
						setv va03 2
						setv va01 1
					endi
				endi
				doif va04 ge 7
					doif lowa subs ptxt 1 7 eq "summon "
						subv va04 7
						sets va02 lowa subs ptxt 8 va04
						setv va03 4
						setv va01 1
					endi
				endi
			endi
		next

* Reset our break variable!
		setv va01 0

* Check if we have a creature name to work with.
		doif va02 ne ""
* See if any creatures match that name.
			enum 4 0 0
* Check our 'break' flag. If we're done, no sense in doing more string comparisons.
				doif va01 eq 0
* Set va04 to the creature's name.
					sets va04 lowa hist name gtos 0
* Check if the name (va04) matches the name given in the speech bubble (va02)
					doif va04 eq va02
* break;
						setv va01 1
* Change the active creature if we're not summoning.
						doif va03 ne 4
							norn targ
						endi
						doif va03 eq 1
* Hold its hand.
							mesg writ targ 13
						elif va03 eq 2 or va03 eq 4
* Pick it up.
							doif va03 eq 2
* If this isn't a summon, we have to wait for the active creature to get set. Otherwise it teleports.
								wait 3
							endi
							inst
* Kind of a kludge, but it works. The default behavior for right-clicking a creature is to hold its hand.
* Here we temporarily override that setting so that right-clicking picks it up.
							setv va04 game "engine_creature_pickup_status"
							setv game "engine_creature_pickup_status" 0
							rgam
* Sent a message to ID 1000 (DS Pointer scripts.cos) saying pntr (hand) wants to pick this creature up.
							mesg wrt+ pntr 1000 targ null 0
							wait 5
							inst
							setv game "engine_creature_pickup_status" va04
							rgam
						endi
					endi
				endi
			next
		endi
	endi
endm

* Removal script. 
rscr
enum 1 1 13816
	kill targ
next
* Remove from scriptorium. In theory. It doesn't actually seem to do anything...
scrx 1 1 13816 73
*dbg: poll