***
* Rotten Eggs
* v1.0
*
* Cause eggs older than 5 hours (customizable via ov00) to rot, contributing some nutrients to the soil.
*
* Author: lisdude@lisdude.com
* Last Modified: 2024-06-26
***

new: simp 1 1 41611 "blnk" 0 0 0
attr 16

* Default rot time: 5 hours
setv ov00 360000

* Every five minutes
tick 6000

scrp 1 1 41611 9
setv va01 ov00
enum 3 4 0
    doif targ ne null
        doif name "laid_time" eq 0
* Inform the egg of what time it was laid. Since we're not modifying the actual egg laying script, we approximate with the current tick.
            setv name "laid_time" wtik
        else
            setv va00 wtik
            subv va00 name "laid_time" 
            doif va00 ge va01
* The egg is older than 5 hours. Kill it and add some nutrients.
* (Someday it might be fun to leave behind a gross sprite and let it slowly decay, but enh.)
                doif room targ ne -1 and carr eq null
                    altr -1 3 0.2
                    altr -1 4 0.2
                endi
                sets va98 "********* Rotted egg at "
                adds va98 vtos posx
                adds va98 " X "
                adds va98 vtos posy
                adds va98 " ("
                setv va95 gmap posx posy
                gsub metaroom_name
                adds va98 va95
                adds va98 ") [diff: "
                adds va98 vtos va00
                adds va98 "] {expected diff: "
                adds va98 vtos va01
                adds va98 "}"
                dbg: outs va98
                kill targ
            endi
        endi
    endi
next

* Take a metaroom number in va95 and return a metaroom name in va95.
* Does it by bruteforce because I don't know if there's a catalogue for this
* and I haven't gotten around to making my array helper and our name variables
* are already being used for metaroom numbers in a different way. Fortunately,
* this is only for debugging purposes. SHRUG.
subr metaroom_name
    doif va95 eq 0
        sets va95 "Norn Terrarium"
    elif va95 eq 1
        sets va95 "Desert Terrarium"
    elif va95 eq 2
        sets va95 "Aquatic Terrarium"
    elif va95 eq 3
        sets va95 "Jungle Terrarium"
    elif va95 eq 4
        sets va95 "Bridge / Engineering"
    elif va95 eq 5
        sets va95 "Pinball"
    elif va95 eq 6
        sets va95 "Grendel Invaders"
    elif va95 eq 7
        sets va95 "Learning Room"
    elif va95 eq 8
        sets va95 "Crypt"
    elif va95 eq 9
        sets va95 "Injector Preview(?)"
    elif va95 eq 10
        sets va95 "Hub"
    elif va95 eq 11
        sets va95 "Norn Meso"
    elif va95 eq 12
        sets va95 "Workshop"
    elif va95 eq 13
        sets va95 "Comms Room"
    elif va95 eq 14
        sets va95 "Injector Preview(?)"
    elif va95 eq 15
        sets va95 "C1 Albia"
    else
        sets va95 "UNKNOWN"
    endi
retn

endm

rscr
enum 1 1 41611
    kill targ
next
scrx 1 1 41611 9
