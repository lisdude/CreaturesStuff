***
* Maintenance Scripts
* v1.0
*
* Perform routine world maintenance, including:
*
* - Clean up zzzz's. (I'm not sure why they accumulate the way they do.)
* - Euthanize Norns that are 1 hour old or older and still in the 'youth' life stage.
* - Make sure the empathic food vendor isn't producing enormous sums of food.
*
* TODO: Incorporate Grazer waste fix and remove old script.
*
* Not really intended for distribution, just personal preference. But if you find
* it handy, go nuts!
*
* Author: lisdude@lisdude.com
* Last Modified: 2021-12-21
***

new: simp 1 1 41605 "blnk" 0 0 0
attr 16

* Check every thirty minutes.
tick 36000

scrp 1 1 41605 9
* Kill young Norns.
	enum 4 0 0
		doif targ ne null
			doif cage eq 3 and tage ge 72000
				sets va00 "Euthanized: "
				adds va00 hist name gtos 0
				dbg: outs va00
				dead
			endi
		endi
	next

* Kill zzzz's
	enum 1 2 28
		doif targ ne null
			kill targ
		endi
	next

* Kill food if there are >= 100
	doif totl 2 8 6 ge 100
		enum 2 8 6
			doif targ ne null
				kill targ
			endi
		next
	endi
endm

rscr
enum 1 1 41605
	kill targ
next
scrx 1 1 41605 9
