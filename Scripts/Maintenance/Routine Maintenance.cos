***
* Maintenance Script
* v1.2
*
* Perform routine world maintenance, including:
*
* - Clean up unassociated zzzz's. (I'm not sure why they accumulate the way they do.)
* - Make sure the empathic food vendor isn't producing enormous sums of food.
* - Limit potions to 5.
* - Dump eggs out of vehicles.
*
* Author: lisdude@lisdude.com
* Last Modified: 2024-06-30
***

new: simp 1 1 41605 "blnk" 0 0 0
attr 16

* Check every fifteen minutes.
tick 18000

scrp 1 1 41605 9

* Kill zzzz's if they don't belong to a creature.
	enum 1 2 28
    	doif targ ne null
            doif type ov00 ne 7
                sets va00 "Invalid zzzz's found at "
                adds va00 vtos posx
                adds va00 " x "
                adds va00 vtos posy
                dbg: outs va00
	        	kill targ
		    endi
        endi
    next

* Keep empathic food to around 100 pieces.
    loop
        rtar 2 8 6
        doif targ ne null
        * Give the room a tiny amount of nutrients when we 'rot' the food.
        doif room targ ne -1 and carr eq null
            altr -1 3 0.003
            altr -1 4 0.003
        endi
            kill targ
        endi
    untl totl 2 8 6 le 100

* Keep potions to a minimum of 5.
    loop
        rtar 2 25 0
        doif targ ne null
            kill targ
        endi
    untl totl 2 25 0 le 5

* Dump eggs that are sitting in vehicles.
    enum 3 1 0
        doif targ ne null
            doif type targ eq 6
                dpas 3 4 0
            endi
        endi
    next

endm

rscr
enum 1 1 41605
	kill targ
next
scrx 1 1 41605 9
