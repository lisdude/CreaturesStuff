***
* Population Indicator
* v1.6
*
* Add indicators for creature count and egg count to the favorite places icons.
*
*
* Author: lisdude@lisdude.com
* Last Modified: 2025-12-07
***

* TODO: Detect when in fast ticks mode and slow updates.

new: simp 1 1 41613 "blnk" 0 0 0
attr 16

* Update every second. Nothing can possibly go wrong!
setv ov00 20
tick ov00

scrp 1 1 41613 9
    inst
    gsub cleanup
    gsub update_favorite_places
    gsub update_creature_count
    gsub update_egg_count
    gsub update_indicators

    * Restore the tick if world start changed it.
    tick ov00

********************************************************************************************

* Iterate through favorite places and update the indicators.
* - va00: The favorite place icon.
* - va01: The metaroom.
* - va02: The creature count.
* - va03: The egg count.
* - va04: The text thingy.
* - va05: The name variable of the metaroom.
subr update_indicators
enum 1 4 0
    doif type name "population_counter" eq 5 and name "population_counter" ne null
        seta va04 name "population_counter"
        seta va00 targ
        setv va01 ov01
        setv va05 va01
* Special snowflake check for engineering room
* (We ensure the known coordinates match the metaroom to be
*  reasonably sure that this is the C3 ship and not another room.)
        doif gmap 4133 3400 eq va01 and spcs eq 5
            mulv va05 -1
        endi
        targ ownr
        setv va02 name va05
        mulv va05 1.0
        setv va03 name va05
        targ va04
        part 1
        doif stoi ptxt ne va02
            mesg wrt+ va04 1000 "creature" va02 0
        endi
        part 2
        doif stoi ptxt ne va03
            mesg wrt+ va04 1000 "egg" va03 1
        endi
    endi
next
retn

* Return the metaroom ID for targ
*   - va00: The metaroom
subr get_metaroom
setv va00 gmap posx posy
* The bridge and engineering rooms are special snowflakes.
* Here we determine if the metaroom is engineering and,
* if so, give it a shiny new negative ID so it can track
* independent of the bridge.
*
* First, pick a set of known coordinates and make sure they're the same metaroom.
doif gmap 4133 3400 eq va00
    * This should confirm that: 1. This is C3. 2. The metaroom is the bridge.
    * NOTE: Since the metaroom icon relies on cameras and resolution assumptions and is inconsistent,
    *       I'm considering engineering to start between the desert and marine terrariums.
    doif posx ge 4300
        * Engineering
        mulv va00 -1
    endi
endi
retn

* Determine how populated each metaroom is.
*  va00: Metaroom ID of current creature.
*  va01: Current value of the metaroom name variable.
subr update_creature_count
enum 4 0 0
    doif targ ne null
        doif dead eq 0
            * Get the creature's current metaroom
                gsub get_metaroom
            * Get the metaroom's current population
                targ ownr
                setv va01 name va00
            * Add one to that population
                addv va01 1
            * Set the variable to the new value.
                setv name va00 va01
        endi
    endi
next
retn

* Determine how many eggs are in each metaroom.
*   - va00: Metaroom ID of the current egg.
*   - va01: Current value of the metaroom name variable.
subr update_egg_count
enum 3 4 0
    doif targ ne null
    * Don't include hatched eggs or eggs in inventory.
        doif pose ne 7 and carr ne game "c3_inventory"
            * Get the current metaroom and convert it to float for storage.
            gsub get_metaroom
            mulv va00 1.0
            * Get the saved population.
            targ ownr
            setv va01 name va00
            * Add one to the total.
            addv va01 1
            * Set the total back.
            setv name va00 va01
        endi
    endi
next
retn

* Update the text of the favorite places icons.
* - va00: The favorite place icon.
* - va01: The text indicator thingy.
subr update_favorite_places
*inst
enum 1 4 0
    doif type name "population_counter" ne 5
        * Create a new indicator object thingy.
        seta va00 targ
        new: comp 1 1 41614 "blank" 1 0 0
        attr 32
        seta va01 targ
        targ va00
        seta name "population_counter" va01
        targ va01
        pat: fixd 1 "blank" 0 0 0 8899 "whiteontransparentchars"
        * Float relative to the favorite place icon.
        frel va00
        * Adjust down from the icon.
        flto 0 30
        part 1
        frmt 6 7 0 0 0 0 0
        ptxt "0"
        pat: fixd 2 "blank" 0 0 0 8899 "whiteontransparentchars"
        part 2
        frmt 6 7 57 0 0 0 1
        ptxt "0"
    endi
next
*slow
retn

* Clean up our name variables.
*  va01: The current name variable.
*  va02: Current count of unviable metarooms.
*  va03: Loop control. CAOS seems to parse both sides of AND, so you can't use it when the second condition relies on the first being true. Sigh.
*  va04: The previous name variable.
subr cleanup
    setv va02 0
    sets va01 ""
*    sets va04 ""
    setv va03 1
    loop
    * Set the previous name variable.
*        doif type va01 eq 2
*            sets va04 va01
*        else
*            setv va04 va01
*        endi

    	namn va01

    * If we got back an empty string, there are no more name variables and we signal the loop to end.
        doif type va01 eq 2
            doif va01 eq ""
                setv va03 0
            endi
        else
            setv name va01 0
*            deln va01
    * We'll need to pass the previous variable back to NAMN to get the correct next one.
*            doif type va04 eq 2
*                sets va01 va04
*            else
*                setv va01 va04
*            endi
        endi
    untl va03 eq 0
retn

endm

* The associations between the text and the favorite places icons seems to get weird
* after reloading. So redo it. sigh.
scrp 1 1 41613 128
inst
enum 1 4 0
    doif type name "population_counter" eq 5
        kill name "population_counter"
        deln "population_counter"
    endi
next
slow
tick 1
endm

* Update the indicator text.
scrp 1 1 41614 1000
    doif _p1_ eq "creature"
        part 1
        ptxt vtos _p2_
    elif _p1_ eq "egg"
        part 2
        ptxt vtos _p2_
    endi
endm

rscr
enum 1 1 41613
    kill targ
next
enum 1 1 41614
    kill targ
next
enum 1 4 0
    deln "population_counter"
next
scrx 1 1 41613 9
scrx 1 1 41613 128
scrx 1 1 41614 1000
